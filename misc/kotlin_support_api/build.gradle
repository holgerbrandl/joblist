buildscript {
    ext.kotlin_version = '1.2.40'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins { id "com.jfrog.bintray" version "1.2" }


apply plugin: 'kotlin'
apply plugin: 'application'
apply plugin: 'maven'
apply plugin: 'maven-publish'

mainClassName = 'foo.BarKt' // not needed but does not work without

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "de.mpicbg.scicomp:joblist:0.7"

    testCompile group: 'junit', name: 'junit', version: '4.11'
}


// http://stackoverflow.com/questions/11474729/how-to-build-sources-jar-with-gradle
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}


//http://stackoverflow.com/questions/34377367/why-is-gradle-install-replacing-my-version-with-unspecified
group 'de.mpicbg.scicomp.joblist'
version '1.2'



publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar { classifier "sources" }

            // just needed because artifact name differs from projet name
            version = project.version
//            artifactId = 'kutils'
            groupId = project.group
        }
    }
}


if (hasProperty('bintray_user') && hasProperty('bintray_key')) {
    bintray {

        // property must be set in ~/.gradle/gradle.properties
        user = bintray_user
        key = bintray_key

        publications = ['maven'] //When uploading configuration files

        dryRun = false //Whether to run this as dry-run, without deploying
        publish = true // If version should be auto published after an upload

        pkg {
            repo = 'mpicbg-scicomp'
            name = 'joblist-kotlin'
            vcsUrl = 'https://github.com/holgerbrandl/joblist'

            licenses = ['MIT']
            publicDownloadNumbers = true

            //Optional version descriptor
            version {
                name = project.version //Bintray logical version name
                desc = '.'
                released = new java.util.Date()
                vcsTag = 'v' + project.version
            }
        }
    }
}